buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.jooq:jooq-codegen:3.6.1'
        classpath 'org.postgresql:postgresql:9.4.1212.jre7'
        classpath 'gradle.plugin.com.boxfuse.client:flyway-release:4.0.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.0.3"
    id 'nu.studer.jooq' version '2.0.2'
}

group 'net.ancientabyss.absimm'

version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

task stage(dependsOn: ['flywayMigrate', 'build', 'clean'])
build.mustRunAfter clean
build.mustRunAfter flywayMigrate

assemble.dependsOn(shadowJar)

jar {
    manifest {
        attributes 'Main-Class': 'Main'
    }
}

configurations {
    compile.extendsFrom generatedCompile
}

sourceSets {
    generated
    main {
        compileClasspath += generated.output
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile group: 'org.telegram', name: 'telegrambots', version: '2.4.3'
    compile name: 'absim-xmpp-1.0-SNAPSHOT'
    compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1212.jre7'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'
    generatedCompile 'org.jooq:jooq:3.6.1',
            'org.jooq:jooq-meta:3.6.1',
            'org.jooq:jooq-codegen:3.6.1'
    generatedCompile group: 'org.flywaydb', name: 'flyway-core', version: '4.0.3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    jooqRuntime 'org.postgresql:postgresql:9.4.1212.jre7'
}


String dbUsername = ""
String dbPassword = ""
String dbUrl = ""

if (!System.getenv('JDBC_DATABASE_URL')?.trim()) {
    // Workaround for getting database configuration in Heroku, see
    // http://stackoverflow.com/questions/27954841/heroku-database-url-as-a-jdbc-url-for-maven
    URI dbUri = new URI(System.getenv("DATABASE_URL"))
    dbUsername = dbUri.getUserInfo().split(":")[0]
    dbPassword = dbUri.getUserInfo().split(":")[1]
    dbUrl = "jdbc:postgresql://" + dbUri.getHost() + ":" + dbUri.getPort() + dbUri.getPath()
} else {
    dbUsername = System.getenv('JDBC_DATABASE_USERNAME')
    dbPassword = System.getenv('JDBC_DATABASE_PASSWORD')
    dbUrl = System.getenv('JDBC_DATABASE_URL')
}


// db migration
flyway {
    user = dbUsername
    password = dbPassword
    url = dbUrl
    driver = 'org.postgresql.Driver'
    schemas = ['public']
}


// code generation
jooq {
    version = '3.8.5'
    edition = 'OSS'
    absimm(sourceSets.main) {
        jdbc {
            user = dbUsername
            password = dbPassword
            url = dbUrl
            driver = 'org.postgresql.Driver'
            schema = 'public'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'net.ancientabyss.data'
            }
        }
    }
}
